import net.fabricmc.loom.api.mappings.layered.MappingContext
import net.fabricmc.loom.api.mappings.layered.MappingLayer
import net.fabricmc.loom.api.mappings.layered.MappingsNamespace
import net.fabricmc.loom.api.mappings.layered.spec.MappingsSpec
import net.fabricmc.loom.configuration.providers.mappings.intermediary.IntermediaryMappingLayer
import net.fabricmc.mappingio.MappingVisitor
import net.fabricmc.mappingio.tree.MemoryMappingTree

plugins {
	id 'fabric-loom' version '0.11-SNAPSHOT'
	id 'maven-publish'
	id 'io.github.juuxel.loom-quiltflower' version '1.6.0'
}

sourceCompatibility = JavaVersion.VERSION_17
targetCompatibility = JavaVersion.VERSION_17

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

repositories {
	maven {
		name = 'ParchmentMC'
		url = 'https://maven.parchmentmc.org'
	}
	maven {
		name = 'GeckoLib'
		url = 'https://dl.cloudsmith.io/public/geckolib3/geckolib/maven/'
	}
//	maven {
//		name = 'Fabric'
//		url = 'https://maven.fabricmc.net/'
//	}
}

// add a new configuration for a transitive `include`
configurations {
	includeTransitive {
		transitive = true
	}
}

dependencies {
//	runtimeOnly "net.fabricmc:fabric-loom:0.11-SNAPSHOT"

	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"

	// auto-generate mappings to stop mojmap conflicts with geckolib
	mappings loom.layered() {
		officialMojangMappings()
		parchment("org.parchmentmc.data:${project.parchment_mappings}@zip")
		// don't convert this to a dynamic instantiation, otherwise java won't see the overridden hashCode
		addLayer(new MappingsSpec<MappingLayer>() {
			final Map<String, String> METHOD_NAME_MAP = Map.of(
					"getTextureLocation", "_getTextureLocation"
			)

			@Override
			MappingLayer createLayer(MappingContext mappingContext) {
				return new MappingLayer() {
					@Override
					void visit(MappingVisitor mappingVisitor) throws IOException {
						MemoryMappingTree memoryMappingTree = mappingVisitor as MemoryMappingTree
						memoryMappingTree.getClasses().forEach(classEntry -> {
							classEntry.methods.forEach(methodEntry -> {
								String newMethodName = METHOD_NAME_MAP.get(methodEntry.getName(MappingsNamespace.NAMED.toString()))
								if (newMethodName != null) {
									//noinspection GroovyAccessibility
									methodEntry.srcName = newMethodName
								}
							})
						})
					}

					@Override
					MappingsNamespace getSourceNamespace() {
						return MappingsNamespace.NAMED
					}

					@Override
					List<Class<? extends MappingLayer>> dependsOn() {
						return List.of(IntermediaryMappingLayer.class)
					}
				}
			}

			@Override
			int hashCode() {
				// used to make sure caches stay consistent
				return METHOD_NAME_MAP.hashCode()
			}
		})
	}

	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// for some reason the native and main jffi clash, so we have to exclude them.
	// we also exclude ASM because loader already bundles it.
	includeTransitive(implementation("org.lmdbjava:lmdbjava:0.8.2")) {
		exclude group: "com.github.jnr", module: "jffi"
		exclude group: "org.ow2.asm"
	}
	// this is set to runtimeOnly because it will otherwise conflict with the main module
	include(runtimeOnly("com.github.jnr:jffi:1.3.9:native"))
	include(implementation("com.github.jnr:jffi:1.3.9"))

	modImplementation "software.bernie.geckolib:geckolib-fabric-1.18:3.0.36"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"
}

// resolve dependencies and add to `include`
// original by Hexafice
afterEvaluate {
	configurations.getByName("includeTransitive").resolvedConfiguration.resolvedArtifacts.forEach(dep -> {
		dependencies.add("include", dep.id.componentIdentifier.displayName)
	})
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

tasks.withType(JavaCompile).configureEach {
	// Minecraft 1.18 (1.18-pre2) upwards uses Java 17.
	it.options.release = 17
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.archivesBaseName}"}
	}
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
